"""lib_imgproc - Image processing library"""

load("@rules_cc//cc:defs.bzl", "cc_library", "cc_test")
load("//bazel:features.bzl", "if_photo_enabled")

package(default_visibility = ["//visibility:public"])

cc_library(
    name = "lib_imgproc",
    srcs = glob(
        [
            "src/*.cpp",
        ],
        allow_empty = True,
    ),
    hdrs = glob(
        [
            "include/imgproc/*.hpp",
            "include/imgproc/*.h",
            "include/imgproc/impl/*.hpp",
            "include/imgproc/impl/*.h",
            "src/*.h",
        ],
        allow_empty = True,
    ),
    defines = if_photo_enabled(["HAVE_OPENCV_PHOTO"]),
    includes = [
        "include",
        "src",
    ],
    deps = [
        "//libs/geometry:lib_geometry",
        "//libs/utility:lib_utility",
        "@opencv",
    ],
)

# Tests
[
    cc_test(
        name = test_file[len("tests/"):-len(".cpp")],
        size = "small",
        srcs = [test_file],
        defines = if_photo_enabled(["HAVE_OPENCV_PHOTO"]),
        deps = [
            ":lib_imgproc",
            "@googletest//:gtest",
            "@googletest//:gtest_main",
        ],
    )
    for test_file in glob(["tests/test_*.cpp"])
]
