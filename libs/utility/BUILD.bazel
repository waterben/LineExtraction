"""lib_utility - Base utility library"""

load("@rules_cc//cc:defs.bzl", "cc_library", "cc_test")
load("//tools/bazel:copts.bzl", "LE_COPTS")

package(default_visibility = ["//visibility:public"])

cc_library(
    name = "lib_utility",
    srcs = glob([
        "src/*.cpp",
    ]),
    hdrs = glob(
        [
            "include/utility/*.hpp",
            "include/utility/*.h",
        ],
        allow_empty = True,
    ),
    copts = LE_COPTS,
    includes = ["include"],
    deps = [
        "@eigen",
        "@opencv//:calib3d",  # Camera calibration (used by camera_utilities.hpp)
        "@opencv//:core",  # Core module - no GUI
        "@opencv//:imgproc",  # Image processing utilities
    ],
)

# GUI utilities - separate library to avoid linking highgui in headless builds
cc_library(
    name = "lib_utility_gui",
    hdrs = ["include/utility/matlab_helpers_gui.hpp"],
    copts = LE_COPTS,
    includes = ["include"],
    deps = [
        ":lib_utility",
        "@opencv//:highgui",  # GUI module for cv::imshow
    ],
)

# Tests
[
    cc_test(
        name = test_file[len("tests/"):-len(".cpp")],
        size = "small",
        srcs = [test_file],
        copts = LE_COPTS,
        deps = [
            ":lib_utility",
            "@googletest//:gtest",
            "@googletest//:gtest_main",
        ],
    )
    for test_file in glob(["tests/test_*.cpp"])
]
